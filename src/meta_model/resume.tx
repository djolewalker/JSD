Resume:
  'Personal info:' personal_info=PersonalInfo
  'Works:' works*=Work
  'Volunteer:' volunteer*=Volunteer
  'Education:' education*=Education
  'Certificates:' certificates*=Certificate
  'Awards:' awards*=Award
  'Publications:' publications*=Publication
  'Skills:' skills*=Skill
  'Languages:' languages*=Language
  'Interests:' interests*=Interest
  'Projects:' projects*=Project
  ('Template:' template=TEMPLATE)?
;

PersonalInfo:
  'Name:' name=STRING
  'Label:' label=STRING
  'Image:' image=URL
  'Email:' email=EMAIL
  'Phone:' phone=PHONE
  'URL:' url=URL
  'Summary:' summary=STRING
  'Location:' location=Location
  'Profiles:' profiles*=Profile
;

Location:
  'Address:' address=STRING
  'City:' city=STRING
  'Country Code:' country_code=COUNTRY_CODE
  ('Postal Code:' postal_code=STRING)?
  ('Region:' region=STRING)?
;

Profile:
  'Network:' network=STRING
  'URL:' url=URL
  ('Username:' username=STRING)?
;

COUNTRY_CODE: /[A-Za-z]{2}/;

Work:
  'Name:' name=STRING
  'Position:' position=STRING
  ('URL:' url=URL)?
  'Duration:' duration=Duration
  'Summary:' summary=STRING
  'Highlights:' highlights*=STRING[',']
;

Volunteer:
  'Organization:' organization=STRING
  'Position:' position=STRING
  'URL:' url=URL
  'Duration:' duration=Duration
  'Summary:' summary=STRING
  'Highlights:' highlights*=STRING[',']
;

Education:
  'Institution:' institution=STRING
  'URL:' url=URL
  'Area:' area=STRING
  'Study type:' study_type=STUDY_TYPE
  'Duration:' duration=Duration
  ('Score:' score=FLOAT)?
  'Courses:' courses*=STRING[',']
;

Award:
  'Title:' title=STRING
  'Date:' date=Date
  'Issuer:' issuer=STRING
  'Summary:' summary=STRING
;

Certificate:
  'Name:' name=STRING
  'Date:' date=Date
  'Issuer:' issuer=STRING
  'URL:' url=URL
;

Publication:
  'Name:' name=STRING
  'Publisher:' publisher=STRING
  'Release date:' date=Date
  'URL:' url=URL
  'Summary:' summary=STRING
;

Skill:
  'Name:' name=STRING
  'Level:' level=STRING
  'Keywords:' keywords*=STRING[',']
;

Language:
  'Language:' language=STRING
  'Fluency:' fluency=STRING
;

Interest:
  'Name:' name=STRING
  'Keywords:' keywords+=STRING[',']
;

Project:
  'Name:' name=STRING
  'Duration:' duration=Duration
  'Description:' description=STRING
  'Highlights:' highlights*=STRING[',']
  'URL:' url=URL
;

Duration: 
  "Start date:" start_date=Date
  ("End date:" end_date=Date)?
;

Date:
  day=DAY '-' month=MONTH '-' year=YEAR | month=MONTH '-' year=YEAR
;

STUDY_TYPE: /Associate|Bachelor|Master|Doctoral/;

DAY: /0[1-9]|[12]\d|3[01]/;

MONTH: /(0|1)[0-9]/;

YEAR: /[0-9]{4}/;

URL: /https?:\/\/(?:www\.)?[-a-zA-Z0-9@:%._\+~#=]{1,256}\.[a-zA-Z0-9()]{1,6}\b(?:[-a-zA-Z0-9()@:%_\+.~#?&\/=]*)/;

PHONE: /[\+]?[(]?[0-9]{3}[)]?[-\s\.]?[0-9]{3}[-\s\.]?[0-9]{4,6}/;

EMAIL: /\S+@\S+\.\S+/;

TEMPLATE: /Kendall|Macciato|Stackoverflow/;
